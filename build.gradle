plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}


group = 'org.scenic-view'
version = '11.0.2'

defaultTasks 'install'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    options.encoding = "UTF-8"
}

application {
    mainClass = "org.scenicview.ScenicView"
    mainModule = "org.scenicview.scenicview"
}

repositories {
    mavenCentral()
}

configurations {
    jdk
    binTray
}

sourceSets {
    main {
        compileClasspath += configurations.jdk
    }
}

javafx {
    modules = ['javafx.web', 'javafx.fxml', 'javafx.swing']
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.scenicview.ScenicView',            /* Used for standalone Scenic View */
                'Agent-Class': 'org.fxconnector.remote.RuntimeAttach',/* But the standalone Scenic View starts up this agent when in runtime attach mode */
                'Premain-Class': 'org.scenicview.ScenicView',          /* Alternatively, this will be called if Scenic View is started as an agent */
                'Automatic-Module-Name': 'org.scenicview.scenicview'
        )
    }
    archiveFileName = 'scenicview.jar'
}

run {
	dependsOn jar
}

artifacts {
    archives(jar)
}

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'scenicView'
    }
    imageDir = file("${buildDir}/scenicview") as Directory
    imageZip = file("${buildDir}/dist/scenicview-${platform}.zip") as RegularFile
}

tasks.jlink.doLast {
    // Copy scenicView.jar to lib, it is required as loading Agent
    copy {
        from "${buildDir}/libs/scenicview.jar"
        into "${buildDir}/scenicview/lib"
    }
}
